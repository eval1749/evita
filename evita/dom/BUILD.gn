# Copyright 2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//evita/dom/idl_files.gni")
import("//evita/dom/js_files.gni")

source_set("dom") {
  sources = [
    "clipboard/clipboard.cc",
    "clipboard/data_transfer.cc",
    "clipboard/data_transfer_data.cc",
    "clipboard/data_transfer_item.cc",
    "clipboard/data_transfer_item_list.cc",
    "converter.cc",
    "css/style.cc",
    "dictionary.cc",
    "editor.cc",
    "encodings/text_decoder.cc",
    "encodings/text_encoder.cc",
    "events/composition_event.cc",
    "events/composition_event.h",
    "events/composition_span.cc",
    "events/composition_span.h",
    "events/document_event.cc",
    "events/event.cc",
    "events/event_target.cc",
    "events/focus_event.cc",
    "events/form_event.cc",
    "events/keyboard_event.cc",
    "events/mouse_event.cc",
    "events/ui_event.cc",
    "events/view_event_handler_impl.cc",
    "events/view_event_target.cc",
    "events/view_event_target_set.cc",
    "events/wheel_event.cc",
    "events/window_event.cc",
    "file_path.cc",
    "forms/button_control.cc",
    "forms/checkbox_control.cc",
    "forms/form.cc",
    "forms/form_control.cc",
    "forms/form_observer.cc",
    "forms/form_window.cc",
    "forms/label_control.cc",
    "forms/radio_button_control.cc",
    "forms/text_field_control.cc",
    "forms/text_field_selection.cc",
    "global.cc",
    "lock.cc",
    "os/abstract_file.cc",
    "os/file.cc",
    "os/process.cc",
    "promise_resolver.cc",
    "script_host.cc",
    "script_thread.cc",
    "text/document.cc",
    "text/document_set.cc",
    "text/mutation_observer.cc",
    "text/mutation_observer_controller.cc",
    "text/mutation_record.cc",
    "text/range.cc",
    "text/regular_expression.cc",
    "text/regular_expression.h",
    "time_stamp.cc",
    "timers/one_shot_timer.cc",
    "timers/one_shot_timer.h",
    "timers/repeating_timer.cc",
    "timers/repeating_timer.h",
    "timers/timer.cc",
    "timers/timer.h",
    "windows/document_window.cc",
    "windows/editor_window.cc",
    "windows/point.cc",
    "windows/rect.cc",
    "windows/selection.cc",
    "windows/table_selection.cc",
    "windows/table_window.cc",
    "windows/text_selection.cc",
    "windows/text_window.cc",
    "windows/window.cc",
    "windows/window_ostream.cc",
    "windows/window_set.cc",

    # TODO(eval1749) We should use "$target_gen_dir" for "v8_string.cc".
    "$root_gen_dir/v8_strings.cc",
    "$target_gen_dir/dom_jslib.cc",
    "../gc/collectable.cc",
    "../gc/collector.cc",
    "../gc/visitable.cc",
  ]

  sources += process_file_template(
          interface_idl_files + dictionary_idl_files,
          [
            "$bindings_gen_dir/v8_glue_{{source_name_part}}.cc",
            "$bindings_gen_dir/v8_glue_{{source_name_part}}.h",
          ])

  deps = [
    ":dom_jslib_cc",
    ":dom_unicode_lib_cc",
    ":dom_v8_strings_cc",
    "//base",
    "//base:i18n",
    "//common",
    "//evita/dom/bindings",
    "//evita/dom/public:domapi",
    "//evita/v8_glue",
    "//regex",
    "//v8",
  ]

  include_dirs = [
    root_gen_dir,
    bindings_gen_dir,
  ]
}

action("dom_jslib_cc") {
  deps = [ ":check_jslib" ]
  script = "make_get_jslib.py"
  outputs = [
    "$target_gen_dir/dom_jslib.cc",
  ]
  inputs = [ "closure_compiler_workaround.js" ] + js_defs_files + js_lib_files

  # TODO(eval1749) We should pass list of JS files to "make_get_js_lib.py"
  # rather than command line.
  args = rebase_path(outputs) + rebase_path(inputs)
}

static_library("dom_unicode_lib_cc") {
  sources = [
    "unicode_icu.cc",
  ]
  deps = [
    "//base:i18n",
    "//v8",
    "//third_party/icu:icuuc",
  ]
}

action("dom_v8_strings_cc") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.

  script = "make_v8_strings.py"

  inputs = [
    "v8_strings.in",
  ]

  # TODO(eval1749) We should use "$target_gen_dir" for "v8_string.cc".
  outputs = [
    "$root_gen_dir/v8_strings.cc",
    "$root_gen_dir/v8_strings.h",
  ]

  args = [ rebase_path("$root_gen_dir/v8_strings") ] +
         rebase_path(inputs, root_build_dir)
}

action("check_jslib") {
  deps = [
    "//evita/dom/bindings:js_externs",
  ]

  js_files = js_defs_files + js_lib_files
  js_file_list = "$root_gen_dir/evita/jslib_file_list.txt"
  write_file(js_file_list, rebase_path(js_files))

  script = "//tools/razzle/closure_compiler.py"
  outputs = [
    "$root_gen_dir/evita/evita_checked.js",
  ]
  inputs = [
             "closure_compiler_workaround.js",
             "$root_gen_dir/evita/evita_js_externs.js",
           ] + js_files
  args = [
    "--js_output_file=" + rebase_path("$root_gen_dir/evita/evita_checked.js"),
    "--flagfile=" + rebase_path(js_file_list),
    "--extern",
    "$root_gen_dir/evita/evita_js_externs.js",
  ]
}

# TODO(eval1749): We should rename to "evita_dom_tests"
test("evita_dom_unittests") {
  sources = [
    "abstract_dom_test.cc",
    "console_unittest.cc",
    "css/style_unittest.cc",
    "editor_unittest.cc",
    "encodings/text_decoder_unittest.cc",
    "encodings/text_encoder_unittest.cc",
    "errors_unittest.cc",
    "events/composition_event_unittest.cc",
    "events/document_event_unittest.cc",
    "events/event_target_unittest.cc",
    "events/event_unittest.cc",
    "events/focus_event_unittest.cc",
    "events/form_event_unittest.cc",
    "events/keyboard_event_unittest.cc",
    "events/mouse_event_unittest.cc",
    "events/ui_event_unittest.cc",
    "events/view_event_handler_impl_unittest.cc",
    "events/wheel_event_unittest.cc",
    "events/window_event_unittest.cc",
    "file_path_unittest.cc",
    "forms/button_control_unittest.cc",
    "forms/checkbox_control_unittest.cc",
    "forms/form_unittest.cc",
    "forms/form_window_unittest.cc",
    "forms/label_control_unittest.cc",
    "forms/radio_button_control_unittest.cc",
    "forms/text_field_control_unittest.cc",
    "forms/text_field_selection_unittest.cc",
    "jslib/ordered_set_unittest.cc",
    "lexers/lexers_unittest.cc",
    "mock_io_delegate.cc",
    "mock_view_impl.cc",
    "os/file_unittest.cc",
    "os/process_unittest.cc",
    "polyfill_unittest.cc",
    "text/document_unittest.cc",
    "text/modes/modes_unittest.cc",
    "text/mutation_observer_unittest.cc",
    "text/range_unittest.cc",
    "text/regular_expression_unittest.cc",
    "text/text_position_unittest.cc",
    "unicode_unittest.cc",
    "windows/editor_window_unittest.cc",
    "windows/point_unittest.cc",
    "windows/rect_unittest.cc",
    "windows/table_selection_unittest.cc",
    "windows/text_selection_unittest.cc",
    "windows/text_window_unittest.cc",
    "windows/window_unittest.cc",
  ]

  deps = [
    "//base/test:test_support",

    # TODO(yosi) We should not have "editor" dependency on
    # "evita_dom_unittests".
    "//evita:editor",
    "//testing/gmock",
    "//testing/gtest",
    "//v8",
  ]
}
