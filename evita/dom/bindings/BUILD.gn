# Copyright 2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//evita/build/js_module_externs.gni")
import("//evita/dom/idl_files.gni")

source_set("bindings") {
  sources = [
    "exception_state.cc",
    "exception_state.h",
    "platform_error.cc",
    "platform_error.h",
  ]

  sources += interface_idl_files + dictionary_idl_files

  public_deps = [
    ":bindings_cc",
    "//gin",
  ]
}

action_foreach("bindings_cc") {
  script = "scripts/idl_to_glue.py"
  sources = interface_idl_files + dictionary_idl_files
  inputs = [
             "scripts/idl_to_glue.py",
             "scripts/code_generator_glue.py",
             "templates/dictionary.cc",
             "templates/dictionary.h",
             "templates/interface.cc",
             "templates/interface.h",
             "templates/js_interface.cc",
             "templates/js_interface.h",
           ] + idl_lexer_parser_files
  outputs = [
    "$bindings_gen_dir/ginx_{{source_name_part}}.cc",
    "$bindings_gen_dir/ginx_{{source_name_part}}.h",
  ]
  args = [
    "--output-dir",
    rebase_path(bindings_gen_dir),
    "--cache-dir",
    rebase_path(target_gen_dir),
    "--info-dir",
    rebase_path(target_gen_dir),
    "--write-file-only-if-changed=1",
    "--target-component=core",
    "{{source}}",
  ]
  deps = [
    ":bindings_externs_js",
  ]
}

js_module_externs("bindings_externs_js") {
  output_dir = "$root_gen_dir/evita/dom"
  output_name = "bindings_externs.js"
  externs = [ "//evita/dom/types_externs.js" ]
  sources = interface_idl_files + dictionary_idl_files
}
