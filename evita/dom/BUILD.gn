# Copyright 2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//evita/build/js_library.gni")
import("//evita/build/js_module.gni")
import("//evita/dom/js_files.gni")
import("//testing/test.gni")
import("//tools/grit/grit_rule.gni")

source_set("dom") {
  sources = [
    "script_thread.cc",
    "script_thread.h",
  ]
  deps = [
    ":core",
    "//base",
    "//evita/base",
    "//evita/dom/scheduler",
    "//evita/ui",
  ]
}

grit("resources") {
  source = "dom_resources.grd"
  resource_ids = "//evita/res/resource_ids"
  outputs = [
    "dom_resources.pak",
    "grit/dom_resources.h",
  ]
  grit_flags = [
    "-E",
    "target_out_dir=" + rebase_path(target_out_dir),
  ]
  deps = [
    ":global_module",
    ":modules",
    "//evita/dom/global:global_strings",
  ]
}

js_library("modules") {
  sources = [
    "${target_out_dir}/components/commands/commands.jsobj",
    "${target_out_dir}/components/find_and_replace/find_and_replace.jsobj",
    "${target_out_dir}/components/highlights/highlights.jsobj",
    "${target_out_dir}/components/launchpad/launchpad.jsobj",
    "${target_out_dir}/components/modes/modes.jsobj",
    "${target_out_dir}/components/spell_checker/spell_checker.jsobj",
    "${target_out_dir}/components/suggestions/suggestions.jsobj",
    "${target_out_dir}/css/css.jsobj",
    "${target_out_dir}/visuals/visuals.jsobj",
  ]
  deps = [
    "components/commands",
    "components/find_and_replace",
    "components/highlights",
    "components/launchpad",
    "components/modes",
    "components/spell_checker",
    "components/suggestions",
    "css:module",
    "visuals:module",
  ]
}

source_set("core") {
  visibility = [ ":*" ]
  sources = [
    "$target_gen_dir/v8_strings.cc",
    "editor.cc",
    "editor.h",
    "file_path.cc",
    "file_path.h",
    "global.cc",
    "global.h",
    "global_load_script.cc",
    "script_host.cc",
    "script_host.h",
    "timers/one_shot_timer.cc",
    "timers/one_shot_timer.h",
    "timers/repeating_timer.cc",
    "timers/repeating_timer.h",
    "timers/timer.cc",
    "timers/timer.h",
    "view_event_handler_impl.cc",
    "view_event_handler_impl.h",
  ]

  deps = [
    ":dom_v8_strings_cc",
    ":global_module",
    ":lock",
    ":resources",
    ":v8_helper",
    "//base",
    "//base:i18n",
    "//common",
    "//evita/dom/bindings",
    "//evita/dom/components/clipboard",
    "//evita/dom/components/imaging",
    "//evita/dom/components/win_registry",
    "//evita/dom/components/win_resource",
    "//evita/dom/css",
    "//evita/dom/encodings",
    "//evita/dom/forms",
    "//evita/dom/os",
    "//evita/dom/public",
    "//evita/dom/scheduler:public",
    "//evita/dom/text",
    "//evita/dom/timing",
    "//evita/dom/unicode",
    "//evita/dom/visuals",
    "//evita/dom/windows",
    "//evita/gc",
    "//evita/gfx/base",
    "//evita/regex",
    "//evita/ui/animation:public",
  ]

  include_dirs = [
    root_gen_dir,
    "$target_gen_dir/bindings",
  ]
}

source_set("lock") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.
  sources = [
    "lock.cc",
    "lock.h",
  ]
  deps = [
    "//base",
  ]
}

source_set("v8_helper") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.
  sources = [
    "converter.cc",
    "converter.h",
    "dictionary.cc",
    "dictionary.h",
    "promise_resolver.cc",
    "promise_resolver.h",
    "time_stamp.cc",
    "time_stamp.h",
  ]
  deps = [
    "//base",
    "//evita/ginx",
    "//evita/text",
  ]
}

# TODO(eval1749): We should move "global_module" to "//evita/dom/global"
js_module("global_module") {
  is_global = true
  files = [
            "global.js",
            "goog.js",
            "closure_compiler_workaround.js",
            "enums.js",
            "events/event_enums.js",
            "strings_en_US.js",
            "unicode/unicode_enums.js",
            "windows/window_enums.js",
          ] + js_lib_files
  externs = [
    "//evita/dom/components/clipboard/clipboard_externs.js",
    "//evita/dom/css/css_externs.js",
    "//evita/dom/os/os_externs.js",
    "$root_gen_dir/evita/dom/css/css_externs.js",
  ]
  deps = [
    "//evita/dom/bindings:bindings_externs_js",
    "//evita/dom/components/highlights:token_state_machines",
    "//evita/dom/css:css_externs",
  ]
}

action("dom_v8_strings_cc") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.

  script = "make_v8_strings.py"

  inputs = [
    "v8_strings.in",
  ]

  outputs = [
    "$target_gen_dir/v8_strings.cc",
    "$target_gen_dir/v8_strings.h",
  ]

  args = [ rebase_path("$target_gen_dir/v8_strings") ] +
         rebase_path(inputs, root_build_dir)
}

test("evita_dom_tests") {
  sources = [
    "editor_unittest.cc",
    "errors_unittest.cc",
    "file_path_unittest.cc",
    "polyfill_unittest.cc",
    "timers/timer_test.cc",
    "view_event_handler_impl_unittest.cc",
  ]

  data = []

  deps = [
    # TODO(eval1749): We should not have "application" dependency on
    # "evita_dom_test".
    "//evita:application",
    "//evita/css",
    "//evita/dom/base:test_files",
    "//evita/dom/components:test_files",
    "//evita/dom/css:test_files",
    "//evita/dom/encodings:test_files",
    "//evita/dom/events:test_files",
    "//evita/dom/forms:test_files",
    "//evita/dom/os:test_files",
    "//evita/dom/testing:test_support",
    "//evita/dom/text:test_files",
    "//evita/dom/timing:test_files",
    "//evita/dom/unicode:test_files",
    "//evita/dom/visuals:test_files",
    "//evita/dom/windows:test_files",
    "//evita/visuals/display:public",
  ]
}
