# Copyright 2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/win/manifest.gni")
import("//testing/test.gni")

group("evita") {
  deps = [
    ":evita_exe",
    "//evita/visuals/demo",
  ]
}

executable("evita_exe") {
  output_name = "evita"
  sources = [
    "ap_main.cc",
    "evita.rc",
    "precomp.cc",
  ]

  deps = [
    ":application",
    "//build/win:default_exe_manifest",
  ]

  if (!is_debug) {
    configs -= [ "//build/config/compiler:default_optimization" ]
    configs += [ "//build/config/compiler:optimize_max" ]
  }

  configs -= [ "//build/config/win:console" ]
  configs += [ "//build/config/win:windowed" ]
}

# TODO(eval1749): Once "evita_dom_unitests" has prices dependency, we should
# not have source_set "application".
source_set("application") {
  sources = [
    "ctrl_TitleBar.cc",
    "vi_EditPane.cc",
    "vi_Frame.cc",
  ]

  public_deps = [
    "//base",
    "//common",
    "//evita/editor",
    "//evita/metrics",
  ]
}

group("evita_tests") {
  testonly = true
  deps = [
    "//evita/base:tests",
    "//evita/dom:evita_dom_tests",
    "//evita/layout:evita_layout_tests",
    "//evita/text:evita_text_tests",
    "//evita/visuals:tests",
  ]
}
