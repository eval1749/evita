# Copyright 2016 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

css_gen_dir = "$target_gen_dir"

source_set("visuals") {
  sources = [
    "$css_gen_dir/css_value_simple_parsers.cc",
    "css_style.cc",
    "css_style.h",
    "css_style_sheet_handle.cc",
    "css_style_sheet_handle.h",
    "css_value_parsers.cc",
    "css_value_parsers.h",
    "node_handle.cc",
    "node_handle.h",
  ]

  public_deps = [
    "//evita/visuals/dom",
  ]

  deps = [
    ":css_properties",
  ]
}

action("css_properties") {
  script = "make_css_style.py"

  inputs = [
    "//evita/visuals/css/properties.in",
    "//evita/visuals/css/css_properties.py",
    "templates/CSSStyleDeclaration.idl",
    "templates/css_style_declaration.js",
    "templates/css_value_simple_parsers.cc",
  ]

  outputs = [
    "$css_gen_dir/CSSStyleDeclaration.idl",
    "$css_gen_dir/css_style_declaration.js",
    "$css_gen_dir/css_value_simple_parsers.cc",
  ]

  args = [
    "--output-directory",
    rebase_path("$css_gen_dir"),
    rebase_path("//evita/visuals/css/properties.in", root_build_dir),
  ]
}

source_set("test_files") {
  testonly = true

  sources = [
    "visuals_test.cc",
  ]

  data = [
    "css_style_sheet_test.js",
    "dom_token_list_test.js",
    "node_test.js",
    "shape_test.js",
  ]

  deps = [
    "//evita/dom/testing:test_support",
  ]
}
