# Copyright (c) 2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

css_gen_dir = "$root_gen_dir/evita/visuals/css"

source_set("css") {
  sources = [
    "$css_gen_dir/properties.cc",
    "$css_gen_dir/properties.h",
    "$css_gen_dir/properties_forward.h",
    "$css_gen_dir/values.cc",
    "$css_gen_dir/values.h",
    "$css_gen_dir/values_forward.h",
    "media.cc",
    "media.h",
    "media_observer.cc",
    "media_observer.h",
    "media_state.cc",
    "media_state.h",
    "media_type.cc",
    "media_type.h",
    "property.cc",
    "property.h",
    "property_set.cc",
    "property_set.h",
    "property_set_builder.cc",
    "property_set_builder.h",
    "property_set_editor.cc",
    "property_set_editor.h",
    "property_visitor.cc",
    "property_visitor.h",
    "rule.cc",
    "rule.h",
    "style.cc",
    "style.h",
    "style_builder.cc",
    "style_builder.h",
    "style_editor.cc",
    "style_editor.h",
    "style_sheet.cc",
    "style_sheet.h",
    "style_sheet_observer.cc",
    "style_sheet_observer.h",
  ]

  public_deps = [
    ":properties",
    ":values",
    "//base",
  ]

  include_dirs = [ root_gen_dir ]
}

action("properties") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.

  script = "make_properties.py"

  inputs = [
    "css_properties.py",
    "properties.in",
    "make_properties.py",
    "templates/properties.cc",
    "templates/properties.h",
    "templates/properties_forward.h",
    "templates/values.cc",
    "templates/values.h",
    "templates/values_forward.h",
  ]

  outputs = [
    "$css_gen_dir/properties.cc",
    "$css_gen_dir/properties.h",
    "$css_gen_dir/properties_forward.h",
    "$css_gen_dir/values.cc",
    "$css_gen_dir/values.h",
    "$css_gen_dir/values_forward.h",
  ]

  args = [
    "--output-directory",
    rebase_path("$css_gen_dir"),
    rebase_path("properties.in", root_build_dir),
  ]
}

source_set("values") {
  sources = [
    "color.cc",
    "color.h",
    "dimension.cc",
    "dimension.h",
    "float_color.cc",
    "float_color.h",
    "length.cc",
    "length.h",
    "percentage.cc",
    "percentage.h",
    "string.cc",
    "string.h",
    "unit.cc",
    "unit.h",
    "value.cc",
    "value.h",
    "value_type.cc",
    "value_type.h",
  ]

  deps = [
    "//base",
  ]
}

source_set("test_supports") {
  testonly = true
  sources = [
    "mock_media.cc",
    "mock_media.h",
  ]
  deps = [
    ":css",
  ]
}

source_set("test_files") {
  testonly = true

  sources = [
    "float_color_test.cc",
    "property_set_test.cc",
    "style_test.cc",
  ]

  public_deps = [
    ":css",
    "//testing/gtest",
  ]
}
