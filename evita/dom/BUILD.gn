# Copyright 2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//evita/dom/idl_files.gni")
import("//evita/dom/js_files.gni")

source_set("dom") {
  sources = [
    "scheduler_impl.cc",
    "scheduler_impl.h",
    "script_thread.cc",
    "script_thread.h",
  ]
  deps = [
    ":components",
    "//base",
    "//evita/ui",
  ]
}

source_set("components") {
  visibility = [ ":*" ]
  sources = [
    "clipboard/clipboard.cc",
    "clipboard/data_transfer.cc",
    "clipboard/data_transfer_data.cc",
    "clipboard/data_transfer_item.cc",
    "clipboard/data_transfer_item_list.cc",
    "css/text_style.cc",
    "css/text_style.h",
    "editor.cc",
    "editor.h",
    "encodings/text_decoder.cc",
    "encodings/text_encoder.cc",
    "events/composition_event.cc",
    "events/composition_event.h",
    "events/composition_span.cc",
    "events/composition_span.h",
    "events/event.cc",
    "events/event_target.cc",
    "events/focus_event.cc",
    "events/form_event.cc",
    "events/keyboard_event.cc",
    "events/mouse_event.cc",
    "events/text_document_event.cc",
    "events/ui_event.cc",
    "events/view_event_handler_impl.cc",
    "events/view_event_target.cc",
    "events/view_event_target_set.cc",
    "events/wheel_event.cc",
    "events/window_event.cc",
    "file_path.cc",
    "file_path.h",
    "forms/button_control.cc",
    "forms/checkbox_control.cc",
    "forms/form.cc",
    "forms/form_control.cc",
    "forms/form_observer.cc",
    "forms/form_window.cc",
    "forms/label_control.cc",
    "forms/radio_button_control.cc",
    "forms/text_field_control.cc",
    "forms/text_field_selection.cc",
    "global.cc",
    "global.h",
    "script_host.cc",
    "script_host.h",
    "timers/one_shot_timer.cc",
    "timers/one_shot_timer.h",
    "timers/repeating_timer.cc",
    "timers/repeating_timer.h",
    "timers/timer.cc",
    "timers/timer.h",
    "windows/editor_window.cc",
    "windows/editor_window.h",
    "windows/point.cc",
    "windows/point.h",
    "windows/rect.cc",
    "windows/rect.h",
    "windows/selection.cc",
    "windows/selection.h",
    "windows/text_document_window.cc",
    "windows/text_document_window.j",
    "windows/text_selection.cc",
    "windows/text_selection.h",
    "windows/text_window.cc",
    "windows/text_window.h",
    "windows/visual_window.cc",
    "windows/visual_window.h",
    "windows/window.cc",
    "windows/window.h",
    "windows/window_ostream.cc",
    "windows/window_set.cc",
    "windows/window_set.h",

    # TODO(eval1749): We should use "$target_gen_dir" for "v8_string.cc".
    "$root_gen_dir/v8_strings.cc",
    "$target_gen_dir/dom_jslib.cc",
  ]

  sources += process_file_template(
          interface_idl_files + dictionary_idl_files,
          [
            "$bindings_gen_dir/v8_glue_{{source_name_part}}.cc",
            "$bindings_gen_dir/v8_glue_{{source_name_part}}.h",
          ])

  deps = [
    ":dom_jslib_cc",
    ":dom_unicode_lib_cc",
    ":dom_v8_strings_cc",
    ":lock",
    ":scheduler",
    ":v8_helper",
    "//base",
    "//base:i18n",
    "//common",
    "//evita/dom/bindings",
    "//evita/dom/os",
    "//evita/dom/public",
    "//evita/dom/text",
    "//evita/dom/timing",
    "//evita/dom/visuals",
    "//evita/gc",
    "//evita/ui/animation:public",
    "//evita/v8_glue",
    "//evita/visuals",
    "//regex",
  ]

  include_dirs = [
    root_gen_dir,
    bindings_gen_dir,
  ]
}

source_set("lock") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.
  sources = [
    "lock.cc",
    "lock.h",
  ]
  deps = [
    "//base",
  ]
}

source_set("v8_helper") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.
  sources = [
    "converter.cc",
    "converter.h",
    "dictionary.cc",
    "dictionary.h",
    "promise_resolver.cc",
    "promise_resolver.h",
    "time_stamp.cc",
    "time_stamp.h",
  ]
  deps = [
    "//base",
    "//evita/text",
    "//evita/v8_glue",
  ]
}

source_set("scheduler") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.
  sources = [
    "scheduler.cc",
    "scheduler.h",
    "scheduler_client.cc",
    "scheduler_client.h",
  ]
  deps = [
    "//base",
  ]
}

action("dom_jslib_cc") {
  deps = [
    ":check_jslib",
    "//evita/dom/visuals:css_properties",
  ]
  script = "make_get_jslib.py"
  outputs = [
    "$target_gen_dir/dom_jslib.cc",
  ]
  inputs = [ "closure_compiler_workaround.js" ] + js_defs_files + js_lib_files

  # TODO(eval1749): We should pass list of JS files to "make_get_js_lib.py"
  # rather than command line.
  args = rebase_path(outputs) + rebase_path(inputs)
}

source_set("dom_unicode_lib_cc") {
  sources = [
    "unicode_icu.cc",
  ]
  deps = [
    "//base:i18n",
    "//third_party/icu:icuuc",
    "//v8",
  ]
}

action("dom_v8_strings_cc") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.

  script = "make_v8_strings.py"

  inputs = [
    "v8_strings.in",
  ]

  # TODO(eval1749): We should use "$target_gen_dir" for "v8_string.cc".
  outputs = [
    "$root_gen_dir/v8_strings.cc",
    "$root_gen_dir/v8_strings.h",
  ]

  args = [ rebase_path("$root_gen_dir/v8_strings") ] +
         rebase_path(inputs, root_build_dir)
}

action("check_jslib") {
  deps = [
    "//evita/dom/bindings:js_externs",
    "//evita/dom/visuals:css_properties",
  ]

  js_files = js_defs_files + js_lib_files
  js_file_list = "$root_gen_dir/evita/jslib_file_list.txt"
  write_file(js_file_list, rebase_path(js_files))

  script = "//tools/razzle/closure_compiler.py"
  outputs = [
    "$root_gen_dir/evita/evita_checked.js",
  ]
  inputs = [
             "closure_compiler_workaround.js",
             "$root_gen_dir/evita/evita_js_externs.js",
           ] + js_files
  args = [
    "--js_output_file=" + rebase_path("$root_gen_dir/evita/evita_checked.js"),
    "--flagfile=" + rebase_path(js_file_list),
    "--extern",
    "$root_gen_dir/evita/evita_js_externs.js",
  ]
}

test("evita_dom_tests") {
  sources = [
    "base/ordered_set_test.cc",
    "css/text_style_test.cc",
    "editor_unittest.cc",
    "encodings/text_decoder_unittest.cc",
    "encodings/text_encoder_unittest.cc",
    "errors_unittest.cc",
    "events/composition_event_unittest.cc",
    "events/event_target_unittest.cc",
    "events/event_unittest.cc",
    "events/focus_event_unittest.cc",
    "events/form_event_unittest.cc",
    "events/keyboard_event_unittest.cc",
    "events/mouse_event_unittest.cc",
    "events/text_document_event_unittest.cc",
    "events/ui_event_unittest.cc",
    "events/view_event_handler_impl_unittest.cc",
    "events/wheel_event_unittest.cc",
    "events/window_event_unittest.cc",
    "file_path_unittest.cc",
    "forms/button_control_unittest.cc",
    "forms/checkbox_control_unittest.cc",
    "forms/form_unittest.cc",
    "forms/form_window_unittest.cc",
    "forms/label_control_unittest.cc",
    "forms/radio_button_control_unittest.cc",
    "forms/text_field_control_unittest.cc",
    "forms/text_field_selection_unittest.cc",
    "lexers/lexers_unittest.cc",
    "mock_io_delegate.cc",
    "mock_view_impl.cc",
    "polyfill_unittest.cc",
    "repl/console_unittest.cc",
    "timers/timer_test.cc",
    "unicode_unittest.cc",
    "windows/editor_window_unittest.cc",
    "windows/point_unittest.cc",
    "windows/rect_unittest.cc",
    "windows/text_selection_unittest.cc",
    "windows/text_window_unittest.cc",
    "windows/window_unittest.cc",
  ]

  deps = [
    # TODO(eval1749): We should not have "application" dependency on
    # "evita_dom_test".
    "//evita:application",
    "//evita/dom/os:test_files",
    "//evita/dom/testing:test_support",
    "//evita/dom/text:test_files",
    "//evita/dom/timing:test_files",
    "//evita/dom/visuals:test_files",
    "//evita/visuals/css",
    "//evita/visuals/display:public",
  ]
}
