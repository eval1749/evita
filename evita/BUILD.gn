# Copyright 2015 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/win/manifest.gni")
import("//testing/test.gni")
import("//tools/grit/repack.gni")

group("evita") {
  deps = [
    ":evita_exe",
    ":evita_resources",
    "//evita/visuals/demo",
  ]
}

executable("evita_exe") {
  output_name = "evita"
  sources = [
    "ap_main.cc",
    "evita.rc",
  ]

  deps = [
    ":application",
    "//evita/res:evita_exe_manifest",
  ]

  if (!is_debug) {
    configs -= [ "//build/config/compiler:default_optimization" ]
    configs += [ "//build/config/compiler:optimize_max" ]
  }

  configs -= [ "//build/config/win:console" ]
  configs += [ "//build/config/win:windowed" ]
}

# TODO(eval1749): Once "evita_dom_unitests" has prices dependency, we should
# not have source_set "application".
source_set("application") {
  public_deps = [
    "//base",
    "//common",
    "//evita/editor",
    "//evita/frames",
    "//evita/metrics",
  ]
}

group("evita_tests") {
  testonly = true
  deps = [
    "//evita/base:tests",
    "//evita/dom:evita_dom_tests",
    "//evita/gfx:tests",
    "//evita/regex:tests",
    "//evita/text:evita_text_tests",
    "//evita/text/layout:evita_layout_tests",
    "//evita/visuals:tests",
  ]
}

repack("evita_resources") {
  output = "$root_out_dir/evita_resources.pak"
  sources = [
    "$target_gen_dir/dom/dom_resources.pak",
  ]
  deps = [
    "//evita/dom:resources",
  ]
}
