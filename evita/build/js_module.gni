# Copyright (c) 2016 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//evita/build/closure_compiler.gni")
import("//tools/grit/grit_rule.gni")

# Generate JavaScript module binary to load it via resource data pack from
# JavaScript source files.
#
# Parameters:
#   externs (optional)
#       A list of JavaScript extern files in addition to DOM global objects.
#   files (optional)
#       A list of JavaScript files into module pack.
#   is_global (optional, default is false)
#   output_name (optional, default is target_name)
#
# Note: Since "gn format" sorts contents of "sources" variable and order of
# files in module is important, we should not use "sources" to specify files
# into a module.
#
# Example:
#   Make "${target_out_dir}/foo_module.bin" containing "foo.js", "foo_init.js",
#   and "foo_util.js".
#
#   js_module("foo_module") {
#     sources = [
#       "foo.js",
#       "foo_init.js",
#       "foo_util.js",
#     ]
#     externs = [
#       "//evita/dom/components/imaging/imaging_externs.js",
#     ]
#     deps = [
#       "//evita/dom/components/imaging",
#     ]
#   }
template("js_module") {
  assert(defined(invoker.files),
         "Need files in $target_name listing the js files.")
  files = invoker.files

  if (defined(invoker.deps)) {
    target_deps = invoker.deps
  } else {
    target_deps = []
  }

  if (defined(invoker.output_name)) {
    output_name = invoker.output_name
  } else {
    output_name = target_name
  }

  checked_js = "${output_name}_checked.js"
  module_obj = "$target_out_dir/${output_name}.jsobj"

  checked_js_module(checked_js) {
    forward_variables_from(invoker,
                           [
                             "externs",
                             "is_global",
                           ])
    output_name = output_name
    sources = files
    deps = target_deps
  }

  action(target_name) {
    response_file_contents = rebase_path(files)
    output_name = "$module_obj"
    script = "//evita/build/scripts/make_js_module_obj.py"
    inputs = files
    outputs = [
      output_name,
    ]
    args = [
      rebase_path(outputs[0]),
      "{{response_file_name}}",
    ]
    deps = [ ":${checked_js}" ] + target_deps
  }
}
