# Copyright (c) 2016 Project Vogue. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

executable("checker") {
  output_name = "joana"

  sources = [
    "$target_gen_dir/ecmascript_externs.cc",
    "checker_main.cc",
  ]

  deps = [
    ":ecmascript_externs",
    "//joana/analyzer/public",
    "//joana/parser/public",
  ]

  if (!defined(ldflags)) {
    ldflags = []
  }

  if (is_win) {
    # Use 5MB stack size for "v8/test/mjsunit/asm/poppler/poppler.js" which
    # is generated by emscripten.
    ldflags = [ "/STACK:5242880" ]
  }
}

action("ecmascript_externs") {
  visibility = [ ":*" ]

  script = "make_externs_cc.py"

  outputs = [
    "$target_gen_dir/ecmascript_externs.cc",
  ]

  inputs = [
    "//joana/externs/es3.js",
    "//joana/externs/es5.js",
    "//joana/externs/es6.js",
    "//joana/externs/es6_collections.js",
  ]

  args = [
           rebase_path(target_gen_dir),
           "EcmaScript",
         ] + rebase_path(inputs)
}
